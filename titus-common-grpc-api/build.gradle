apply plugin: 'com.google.protobuf'

buildscript {
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
    }
    repositories {
        jcenter()
    }
}

configurations {
    protobuf
    protoPluginDeps {
        description = 'used to resolve grpc versions from recommendations for reference in proto plugin'
    }
}

dependencies {
    // FIXME We generate classes for the same schema twice, as there is no easy way to separate them.
    protobuf "com.netflix.titus:titus-api-definitions:${titusApiDefinitionsVersion}"

    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "com.google.protobuf:protobuf-java:${protobufVersion}"

    protoPluginDeps "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    protoPluginDeps "com.google.protobuf:protoc:${protobufVersion}"
}

if (project.hasProperty("idlLocal")) {
    task extractProto {
        doLast {
            copy {
                from '../../titus-api-definitions/src/main/proto'
                into new File(buildDir, "extracted-protos/main")
            }
        }
    }
}

idea {
    module {
        excludeDirs -= file(buildDir)
        buildDir.listFiles({ d, f -> f != 'generated' } as FilenameFilter).each { excludeDirs += it }
    }
}

sourceSets {
    main {
        java {
            srcDir "${protobuf.generatedFilesBaseDir}/main/grpc"
            srcDir "${protobuf.generatedFilesBaseDir}/main/java"
        }
    }
}

check.dependsOn.remove(test)

/*
 * Copyright 2019 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

//Grabs the resolved dependency coordinates so we can pass them to the protobuf plugin with the platform-recommended version
def getResolvedDependencyFromProtoPluginDeps(dependency) {
    configurations.protoPluginDeps.resolvedConfiguration.firstLevelModuleDependencies.collect { it.name }.find {
        it.startsWith(dependency)
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}