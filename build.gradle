buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-netflixoss-project-plugin:5.0.0'
        classpath 'com.netflix.nebula:nebula-ospackage-plugin:3.+'
    }
}

allprojects {
    apply plugin: 'nebula.netflixoss'
    apply plugin: 'nebula.dependency-lock'
    apply plugin: 'idea'

    if (project.hasProperty('useMavenLocal')) {
        repositories {
            mavenLocal()
        }
    }

    repositories {
        jcenter()
        maven { url 'https://dl.bintray.com/netflixoss/oss-candidate/' }
    }

    project.tasks.withType(org.jfrog.gradle.plugin.artifactory.task.BuildInfoBaseTask) { Task task ->
        task.enabled = false
    }
}

subprojects {
    group "com.netflix.titus"

    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        titusApiDefinitionsVersion = '0.0.1-rc66'

        awsSdkVersion = '1.11.+'
        javaxElVersion = '3.+'
        // pinned because of SSL incompatibilities with tomcat-embed-core 9.0.17
        springBootVersion = '2.1.3.RELEASE'
        springCloudVersion = '2.1.1.RELEASE'
        springReactor = '3.1.+'
        springReactorNetty = '0.8.+'
        springVersion = '5.1.+'
        javaxInjectVersion = '1'
        guavaVersion = '20.+'
        servletVersion = '3.1.0'
        rxJava = '1.+'
        hdrhistogramVersion = '2.1.+'
        hsqldbVersion = '2.4.+'
        apacheCommonsCliVersion = '1.3.+'
        archaius2Version = '2.3.+'
        eurekaVersion = '1.+'
        numerusVersion = '1.1'
        snappyVersion = '1.1.+'
        jacksonVersion = '2.9.+'
        slf4jVersion = '1.7.0'
        cliParserVersion = '1.1.1'
        curatorVersion = '2.13.+'
        governatorVersion = '1.15.+'
        jettyVersion = '9.2.12.v20150709'
        jerseyVersion = '1.19.1'
        jooqVersion = '3.+'
        fenzoVersion = '1.1.0+'
        spectatorVersion = '0.59.+'
        mesosVersion = '1.7.2'
        protobufVersion = '3.5.+'
        postgresqlVersion = '9.4.+'
        grpcVersion = '1.10.+'
        googleTruthVersion = '0.34' // pinned to avoid a transitive dependency on guava > 20.x
        swaggerVersion = '1.5.12'
        jsonVersion = '20140107'
        guiceVersion = '4.1.+'
        hibernateValidatorVersion = '5.4.+'
        runtimeHealthVersion = '1.1.+'
        configMagicVersion = '0.11'
        swaggerUiVersion = '2.1.4'
        okHttpVersion = '3.8.0'
        cassandraDriverVersion = '3.3.+'
        commonsCliVersion = '1.3.+'
        commonsCodecVersion = '1.13'
        caffeineVersion = '2.6.+'
        rxJavaInteropVersion = '0.13.+'
        kubernetesClientVersion = '5.0.0'

        // Test
        junitVersion = '4.10'
        mockitoVersion = '2.+'
        hamcrestVersion = '1.3'
        awaitilityVersion = '1.7.0'
        assertjVersion = '3.8.0'
        cassandraUnitVersion = '3.1.1.0'
        mockServerVersion = '3.10.4'
        javaslangVersion = '2.0.6'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << '-parameters'
    }

    tasks.withType(Javadoc).all {
        enabled = false
    }

    // There is some clean up in our transitive dependencies that we need to do here.
    configurations.all {
        exclude group: 'netflix', module: 'SRWrapper'
        exclude group: 'org.apache.ant', module: 'ant'
        exclude group: 'org.apache.cxf', module: 'cxf'
        exclude group: 'javax.servlet', module: 'servlet-api'
        exclude group: 'org.mortbay.jetty', module: 'servlet-api'
        // Superceded by governator-api
        exclude group: 'com.netflix.governator', module: 'governator-annotations'
        resolutionStrategy {
            force 'org.bouncycastle:bcprov-jdk15on:1.50'
            // remove this pin once we fix the jackson version issues
            force 'com.fasterxml.jackson.core:jackson-databind:2.9.9.1'
        }

        // We need to exclude logback as it clashes with other logging engines.
        exclude group: 'ch.qos.logback', module: 'logback-classic'
        exclude group: 'ch.qos.logback', module: 'logback-core'
    }

    dependencies {
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        runtime "org.slf4j:slf4j-log4j12:${slf4jVersion}"

        testCompile "junit:junit-dep:${junitVersion}"
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
        testCompile "org.hamcrest:hamcrest-core:${hamcrestVersion}"
        testCompile "com.jayway.awaitility:awaitility:${awaitilityVersion}"
        testCompile "io.projectreactor:reactor-test:${springReactor}"

        testCompile "org.assertj:assertj-core:${assertjVersion}"
    }

    test {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

        useJUnit {
            excludeCategories 'com.netflix.titus.testkit.junit.category.IntegrationTest', 'com.netflix.titus.testkit.junit.category.IntegrationNotParallelizableTest',
                    'com.netflix.titus.testkit.junit.category.RemoteIntegrationTest'
        }

        reports {
            junitXml.enabled = true
            html.enabled = true
        }

        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    task integrationTest(type: Test) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

        useJUnit {
            includeCategories 'com.netflix.titus.testkit.junit.category.IntegrationTest'
        }

        reports {
            junitXml.enabled = true
            html.enabled = true
        }

        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    task integrationNotParallelizableTest(type: Test) {
        useJUnit {
            includeCategories 'com.netflix.titus.testkit.junit.category.IntegrationNotParallelizableTest'
        }

        reports {
            junitXml.enabled = true
            html.enabled = true
        }

        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    task remoteIntegrationTest(type: Test) {
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

        useJUnit {
            includeCategories 'com.netflix.titus.testkit.junit.category.RemoteIntegrationTest'
        }

        reports {
            junitXml.enabled = true
            html.enabled = true
        }

        testLogging {
            events "failed"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }

    task testAll {
        dependsOn 'test'
        dependsOn 'integrationTest'
        dependsOn 'integrationNotParallelizableTest'
        tasks.findByName('integrationTest').mustRunAfter 'test'
        tasks.findByName('integrationNotParallelizableTest').mustRunAfter 'test'
    }
}

dependencyLock {
    includeTransitives = true
}
